---
layout: post
title: "Arduino 5x7 Matrix Controller"
tagline: Using HDSP-4403 LED Matrix
description: "How to control an HDSP-4403 5x7 LED Matrix with an Arduino"
thumbnail: "/images/thumbnails/5x7.jpg"
category: 
tags: []
---
{% include JB/setup %}
I began by taking a power supply and running 10mA across each possible combination of the twelve pins. Since they are diodes, it mattered which direction the current flows, so there is, I believe, 12^2*2=288 possible combinations. Anyways, I eventually found what pin combinations light up what LEDs, and the trend was, each row corresponds to a pin connected to ground, and each column corresponds to a pin connected to the power source. I lost the original document, but this is the new one I sketched up that (each pin corresponds to a number on a breadboard)

The first thing I did was display a static image on the matrix, which I unfortunately didn’t take a photo of. I learned that you must draw one column or row at a time, if you do not want to draw an image that is mirrored around the center LED.

After I promptly decided I wasn’t interested in making my own font for all of the ASCII letters, I found one online from [this bike speedometer](http://www.openobject.org/opensourceurbanism/Spoke-o-dometer_code) project that I found. For timing, the scrolling characters, I used the [Timer library](http://playground.arduino.cc/code/timer) from Arduino Playground.


<center><iframe width="100%" height="400" src="//www.youtube.com/embed/WqKS3e5PJI0" frameborder="0" allowfullscreen></iframe></center>


Here is the source code:

{% highlight c %}
/*
 * Title: HDSP-4403 LED controller
 * Author: Michael Vartan
 * Contact: admin@mvartan.com
 * Date: 7 March 2013
 */
#include "Timer.h"
 
const int CHARACTER_TIME = 400;
const int X[5] = {6,5,4,3,2};
const int Y[7] = {13,12,11,10,9,8,7};
const char* DISPLAY_MESSAGE = " Made by mvartan.com";
 
/***************************************************************
 * Font taken from:                                            *
 * http://www.openobject.org/opensourceurbanism/Bike_POV_Beta_4*
 ***************************************************************/
const byte font[][5] = {
{},{},{},{},{},{},{},{},
{},{},{},{},{},{},{},{},
{},{},{},{},{},{},{},{},
{},{},{},{},{},{},{},{},
{0x00,0x00,0x00,0x00,0x00}, // 0x20 32
{0x00,0x00,0x6f,0x00,0x00}, // ! 0x21 33
{0x00,0x07,0x00,0x07,0x00}, // " 0x22 34
{0x14,0x7f,0x14,0x7f,0x14}, // # 0x23 35
{0x00,0x07,0x04,0x1e,0x00}, // $ 0x24 36
{0x23,0x13,0x08,0x64,0x62}, // % 0x25 37
{0x36,0x49,0x56,0x20,0x50}, // & 0x26 38
{0x00,0x00,0x07,0x00,0x00}, // ' 0x27 39
{0x00,0x1c,0x22,0x41,0x00}, // ( 0x28 40
{0x00,0x41,0x22,0x1c,0x00}, // ) 0x29 41
{0x14,0x08,0x3e,0x08,0x14}, // * 0x2a 42
{0x08,0x08,0x3e,0x08,0x08}, // + 0x2b 43
{0x00,0x50,0x30,0x00,0x00}, // , 0x2c 44
{0x08,0x08,0x08,0x08,0x08}, // - 0x2d 45
{0x00,0x60,0x60,0x00,0x00}, // . 0x2e 46
{0x20,0x10,0x08,0x04,0x02}, // / 0x2f 47
{0x3e,0x51,0x49,0x45,0x3e}, // 0 0x30 48
{0x00,0x42,0x7f,0x40,0x00}, // 1 0x31 49
{0x42,0x61,0x51,0x49,0x46}, // 2 0x32 50
{0x21,0x41,0x45,0x4b,0x31}, // 3 0x33 51
{0x18,0x14,0x12,0x7f,0x10}, // 4 0x34 52
{0x27,0x45,0x45,0x45,0x39}, // 5 0x35 53
{0x3c,0x4a,0x49,0x49,0x30}, // 6 0x36 54
{0x01,0x71,0x09,0x05,0x03}, // 7 0x37 55
{0x36,0x49,0x49,0x49,0x36}, // 8 0x38 56
{0x06,0x49,0x49,0x29,0x1e}, // 9 0x39 57
{0x00,0x36,0x36,0x00,0x00}, // : 0x3a 58
{0x00,0x56,0x36,0x00,0x00}, // ; 0x3b 59
{0x08,0x14,0x22,0x41,0x00}, // < 0x3c 60 
{0x14,0x14,0x14,0x14,0x14}, // = 0x3d 61 
{0x00,0x41,0x22,0x14,0x08}, // > 0x3e 62
{0x02,0x01,0x51,0x09,0x06}, // ? 0x3f 63
{0x3e,0x41,0x5d,0x49,0x4e}, // @ 0x40 64
{0x7e,0x09,0x09,0x09,0x7e}, // A 0x41 65
{0x7f,0x49,0x49,0x49,0x36}, // B 0x42 66
{0x3e,0x41,0x41,0x41,0x22}, // C 0x43 67
{0x7f,0x41,0x41,0x41,0x3e}, // D 0x44 68
{0x7f,0x49,0x49,0x49,0x41}, // E 0x45 69
{0x7f,0x09,0x09,0x09,0x01}, // F 0x46 70
{0x3e,0x41,0x49,0x49,0x7a}, // G 0x47 71
{0x7f,0x08,0x08,0x08,0x7f}, // H 0x48 72
{0x00,0x41,0x7f,0x41,0x00}, // I 0x49 73
{0x20,0x40,0x41,0x3f,0x01}, // J 0x4a 74
{0x7f,0x08,0x14,0x22,0x41}, // K 0x4b 75
{0x7f,0x40,0x40,0x40,0x40}, // L 0x4c 76
{0x7f,0x02,0x0c,0x02,0x7f}, // M 0x4d 77
{0x7f,0x04,0x08,0x10,0x7f}, // N 0x4e 78
{0x3e,0x41,0x41,0x41,0x3e}, // O 0x4f 79
{0x7f,0x09,0x09,0x09,0x06}, // P 0x50 80
{0x3e,0x41,0x51,0x21,0x5e}, // Q 0x51 81
{0x7f,0x09,0x19,0x29,0x46}, // R 0x52 82
{0x46,0x49,0x49,0x49,0x31}, // S 0x53 83
{0x01,0x01,0x7f,0x01,0x01}, // T 0x54 84
{0x3f,0x40,0x40,0x40,0x3f}, // U 0x55 85
{0x0f,0x30,0x40,0x30,0x0f}, // V 0x56 86
{0x3f,0x40,0x30,0x40,0x3f}, // W 0x57 87
{0x63,0x14,0x08,0x14,0x63}, // X 0x58 88
{0x07,0x08,0x70,0x08,0x07}, // Y 0x59 89
{0x61,0x51,0x49,0x45,0x43}, // Z 0x5a 90
{0x3c,0x4a,0x49,0x29,0x1e}, // [ 0x5b 91
{0x02,0x04,0x08,0x10,0x20}, // \ 0x5c 92
{0x00,0x41,0x7f,0x00,0x00}, // ] 0x5d 93
{0x04,0x02,0x01,0x02,0x04}, // ^ 0x5e 94
{0x40,0x40,0x40,0x40,0x40}, // _ 0x5f 95
{0x00,0x00,0x03,0x04,0x00}, // ` 0x60 96
{0x20,0x54,0x54,0x54,0x78}, // a 0x61 97
{0x7f,0x48,0x44,0x44,0x38}, // b 0x62 98
{0x38,0x44,0x44,0x44,0x20}, // c 0x63 99
{0x38,0x44,0x44,0x48,0x7f}, // d 0x64 100
{0x38,0x54,0x54,0x54,0x18}, // e 0x65 101
{0x08,0x7e,0x09,0x01,0x02}, // f 0x66 102
{0x0c,0x52,0x52,0x52,0x3e}, // g 0x67 103
{0x7f,0x08,0x04,0x04,0x78}, // h 0x68 104
{0x00,0x44,0x7d,0x40,0x00}, // i 0x69 105
{0x20,0x40,0x44,0x3d,0x00}, // j 0x6a 106
{0x00,0x7f,0x10,0x28,0x44}, // k 0x6b 107
{0x00,0x41,0x7f,0x40,0x00}, // l 0x6c 108
{0x7c,0x04,0x18,0x04,0x78}, // m 0x6d 109
{0x7c,0x08,0x04,0x04,0x78}, // n 0x6e 110
{0x38,0x44,0x44,0x44,0x38}, // o 0x6f 111
{0x7c,0x14,0x14,0x14,0x08}, // p 0x70 112
{0x08,0x14,0x14,0x18,0x7c}, // q 0x71 113
{0x7c,0x08,0x04,0x04,0x08}, // r 0x72 114
{0x48,0x54,0x54,0x54,0x20}, // s 0x73 115
{0x04,0x3f,0x44,0x40,0x20}, // t 0x74 116
{0x3c,0x40,0x40,0x20,0x7c}, // u 0x75 117
{0x1c,0x20,0x40,0x20,0x1c}, // v 0x76 118
{0x3c,0x40,0x30,0x40,0x3c}, // w 0x77 119
{0x44,0x28,0x10,0x28,0x44}, // x 0x78 120
{0x0c,0x50,0x50,0x50,0x3c}, // y 0x79 121
{0x44,0x64,0x54,0x4c,0x44}, // z 0x7a 122
{0x00,0x08,0x36,0x41,0x41}, // { 0x7b 123
{0x00,0x00,0x7f,0x00,0x00}, // | 0x7c 124
{0x41,0x41,0x36,0x08,0x00}, // } 0x7d 125
{0x04,0x02,0x04,0x08,0x04}, // ~ 0x7e 126
};
 
int messageLength=0;
int time = 0;
Timer t;
 
void setup() {  
  messageLength = strlen(DISPLAY_MESSAGE);
  int tickEvent = t.every(CHARACTER_TIME/5, moveColumn); //will cycle through the character in five moveColumn()s
  for(int i=2;i<14;i++)
    pinMode(i, OUTPUT);           // set pin to input
  clearScreen(); //sets the pins to their off state
}
//shifts the time variable, to move everything by one column
void moveColumn() {
  time = (time+1)%((messageLength)*5); 
}
 
void loop() {
  int lastSpace = -1; //used to add spaces between characters
  t.update();
  int currentTime = time; //is manipulated when we add spaces between characters.
  for(int column=0;column<5;column++) {
    int currentLetter = ((currentTime+column)/5); // gets the number of letter we are on. 
    int letterColumn = (currentTime+column)%5; //gets the column number on the letter that we will display on the LED column
    if(letterColumn==0 && lastSpace!=letterColumn) { //adds a space before a new character
      lastSpace=letterColumn;
      currentTime-=1;
      continue;
    }
    char currentCharacter = DISPLAY_MESSAGE[currentLetter]; //gets the ASCII value of the current letter.
    byte fontColumn =  font[currentCharacter][letterColumn]; //retrieves from the 2D array font
    digitalWrite(X[column],HIGH); //activates the current column for lighting
    for(int row=0;row<7;row++) {
      int bitValue = !!(fontColumn & (1 << row)); //retrieves bits out of the byte fontColumn
      if(bitValue==1) { 
        digitalWrite(Y[row],LOW); //activate row when bitValue == 1
      }
    }
    delay(1); //seems to make the LED appear brighter with a 1ms delay here.
    for(int i=0;i<7;i++)
      digitalWrite(Y[i],HIGH); //deactivates row
 
    digitalWrite(X[column],LOW); //deactivates column
  }
}
//deactivates all LED rows/columns
void clearScreen() {
  for(int i=0;i<5;i++) {
    digitalWrite(X[i],LOW);
  }
  for(int i=0;i<7;i++) {
    digitalWrite(Y[i],HIGH);
  }
}

{% endhighlight %}